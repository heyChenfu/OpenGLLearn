#version 330 core
out vec4 FragColor;
in vec2 TexCoords;

uniform sampler2D image;

uniform bool horizontal; //当前进行水平还是竖直模糊

uniform float weight[5] = float[] (0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216); //高斯模糊参数

void main()
{
	//计算偏移量
	vec2 tex_offset = 1.0 / textureSize(image, 0);
	vec3 color = texture(image, TexCoords).rgb * weight[0];
	//两步高斯模糊
	if(horizontal){
		for(int i = 1; i < 5; i++){
			color += texture(image, TexCoords + vec2(tex_offset.x * i, 0)).rgb * weight[i];
			color += texture(image, TexCoords - vec2(tex_offset.x * i, 0)).rgb * weight[i];
		}
	}
	else{
		for(int i = 1; i < 5; i++){
			color += texture(image, TexCoords + vec2(0, tex_offset.y * i)).rgb * weight[i];
			color += texture(image, TexCoords - vec2(0, tex_offset.y * i)).rgb * weight[i];
		}
	}
	FragColor = vec4(color, 1.0);
}